plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.91'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories.clear()
repositories {
    // Mirror Maven
    maven {
        url = "https://neoforged.forgecdn.net/releases"
    }
    maven {
        url = "https://neoforged.forgecdn.net/mojang-meta"
    }

    maven {
        url = "https://maven.minecraftforge.net/"
    }
    maven {
        url = "https://libraries.minecraft.net/"
    }

    mavenLocal()

    maven {
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven {
        url = 'https://jitpack.io'
    }

    maven {
        name "KosmX's maven"
        url 'https://maven.kosmx.dev/'
    }

    flatDir {
        dir 'libs'
    }
}

base {
    archivesName = "${mod_file_name}-${minecraft_version}"
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            gameDirectory = project.file('run/client')
            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            gameDirectory = project.file('run/server')
            programArgument '--nogui'
            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            gameDirectory = project.file('run/gametest')
            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            gameDirectory = project.file('run/data')
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG

            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'mixin.debug.export', 'true'
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}
obfuscation {
    createRemappingConfiguration(configurations.localRuntime)
}

dependencies {
    modImplementation "maven.modrinth:slashblade-resharped:${slash_blade_version}"
    modImplementation "maven.modrinth:2zhQmVfn:${mrqx_slash_blade_core_version}"
    modImplementation "maven.modrinth:true-power:${true_power_version}"
    modImplementation "dev.kosmx.player-anim:player-animation-lib-forge:1.0.2-rc1+1.20"
    modImplementation "curse.maven:slashblade-patchouli-1406418:7343612"

    modLocalRuntime "curse.maven:rough-blade-1218145:7411635"
    modLocalRuntime "maven.modrinth:octo-lib:HZ7KmyXp"
    modLocalRuntime "maven.modrinth:architectury-api:9.2.14+forge"

    modCompileOnlyApi "vazkii.patchouli:Patchouli:${patchouli_version}:api"
    modLocalRuntime "vazkii.patchouli:Patchouli:${patchouli_version}"
    modImplementation "com.github.Reimnop:PGen:1.1.0-1.20.1"

    modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    modLocalRuntime "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"

    // Test
    modLocalRuntime "maven.modrinth:moonlight:1.20-2.16.1-forge"
    modLocalRuntime "maven.modrinth:mmmmmmmmmmmm:1.20-2.0.9"

    implementation "org.jetbrains:annotations:17.0.0"
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0"))
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json"
    ])
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version                  : minecraft_version,
            minecraft_version_range            : minecraft_version_range,
            forge_version                      : forge_version,
            forge_version_range                : forge_version_range,
            loader_version_range               : loader_version_range,
            mod_id                             : mod_id,
            mod_name                           : mod_name,
            mod_license                        : mod_license,
            mod_version                        : mod_version,
            mod_authors                        : mod_authors,
            mod_description                    : mod_description,
            slash_blade_version                : slash_blade_version,
            slash_blade_version_range          : slash_blade_version_range,
            mrqx_slash_blade_core_version      : mrqx_slash_blade_core_version,
            mrqx_slash_blade_core_version_range: mrqx_slash_blade_core_version_range,
            true_power_version                 : true_power_version,
            true_power_version_range           : true_power_version_range,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
legacyForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
